apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'


repositories {
    flatDir {
        dirs 'libs'
    }
}


// Load application build configuration.
def buildProperties = new Properties()
def buildProdFile = new File('build_prod.properties')
if (buildProdFile.canRead()) {
    buildProperties.load(new FileInputStream(buildProdFile))
} else {
    buildProdFile = new File('build_default.properties')
    buildProperties.load(new FileInputStream(buildProdFile))
}

android {
    namespace 'com.markupartist.sthlmtraveling'

    final BASE_PROVIDER_DEBUG = "com.markupartist.sthlmtraveling.debug"
    final BASE_PROVIDER_RELEASE = "com.markupartist.sthlmtraveling"

    compileSdk 35
    useLibrary 'org.apache.http.legacy'

    buildFeatures {
        buildConfig = true
    }


    defaultConfig {
        minSdk 23
        targetSdk 35
        applicationId "com.markupartist.sthlmtraveling"
        versionName "2.21.0"
        versionCode 217

        buildConfigField "boolean", "APP_IS_ADS_ENABLED", buildProperties['IS_ADS_ENABLED']
        buildConfigField "String", "APP_WIDESPACE_SID", buildProperties['WIDESPACE_SID']
        buildConfigField "String", "APP_WIDESPACE_DEBUG_SID", buildProperties['WIDESPACE_DEBUG_SID']
        buildConfigField "String", "APP_STHLMTRAVELING_API_KEY", buildProperties['STHLMTRAVELING_API_KEY']
        buildConfigField "String", "APP_ANALYTICS_PROPERTY_KEY", buildProperties['ANALYTICS_PROPERTY_KEY']

        resValue "string", "google_maps_api_key", buildProperties['GOOGLE_MAPS_API_KEY']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
            '-Xjvm-default=all',
            '-opt-in=kotlin.RequiresOptIn'
        ]
    }

    signingConfigs {
        release {
            storeFile file(System.getenv().containsKey("STORE_FILE") ? System.getenv("STORE_FILE") : "dummy.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = buildProperties['IS_CRASHLYTICS_ENABLED'].toBoolean()
            debuggable true
            applicationIdSuffix ".debug"
            buildConfigField "String", "BASE_PROVIDER_AUTHORITY", "\"" + BASE_PROVIDER_DEBUG + "\""
        }

        release {
            ext.enableCrashlytics = buildProperties['IS_CRASHLYTICS_ENABLED'].toBoolean()
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            buildConfigField "String", "BASE_PROVIDER_AUTHORITY", "\"" + BASE_PROVIDER_RELEASE + "\""
        }
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
    }


    packaging {
        resources {
            excludes += ['META-INF/pom.properties', 'META-INF/pom.xml']
        }
    }
}


dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.9.21'
    implementation 'com.google.android.gms:play-services-base:18.2.0'
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
}
