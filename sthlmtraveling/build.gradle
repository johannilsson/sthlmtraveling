apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


repositories {
    flatDir {
        dirs 'libs'
    }
}


// Load application build configuration.
def buildProperties = new Properties()
def buildProdFile = new File('build_prod.properties')
if (buildProdFile.canRead()) {
    buildProperties.load(new FileInputStream(buildProdFile))
} else {
    buildProdFile = new File('build_default.properties')
    buildProperties.load(new FileInputStream(buildProdFile))
}

android {
    final BASE_PROVIDER_DEBUG = "com.markupartist.sthlmtraveling.debug"
    final BASE_PROVIDER_RELEASE = "com.markupartist.sthlmtraveling"

    compileSdkVersion 28
    useLibrary 'org.apache.http.legacy'


    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        applicationId "com.markupartist.sthlmtraveling"
        versionName "2.20.1"
        versionCode 213

        buildConfigField "boolean", "APP_IS_ADS_ENABLED", buildProperties['IS_ADS_ENABLED']
        buildConfigField "String", "APP_WIDESPACE_SID", buildProperties['WIDESPACE_SID']
        buildConfigField "String", "APP_WIDESPACE_DEBUG_SID", buildProperties['WIDESPACE_DEBUG_SID']
        buildConfigField "String", "APP_STHLMTRAVELING_API_KEY", buildProperties['STHLMTRAVELING_API_KEY']
        buildConfigField "String", "APP_ANALYTICS_PROPERTY_KEY", buildProperties['ANALYTICS_PROPERTY_KEY']
        buildConfigField "String", "ADMOB_ROUTE_DETAILS_AD_UNIT_ID", buildProperties['ADMOB_ROUTE_DETAILS_AD_UNIT_ID']
        buildConfigField "String", "ADMOB_TEST_DEVICES", buildProperties['ADMOB_TEST_DEVICES']
        buildConfigField "String", "ADMOB_PUBLISHER", buildProperties['ADMOB_PUBLISHER']

        resValue "string", "google_maps_api_key", buildProperties['GOOGLE_MAPS_API_KEY']
        resValue "string", "admob_app_id", buildProperties['ADMOB_APP_ID']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(System.getenv().containsKey("STORE_FILE") ? System.getenv("STORE_FILE") : "dummy.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = buildProperties['IS_CRASHLYTICS_ENABLED'].toBoolean()
            debuggable true
            applicationIdSuffix ".debug"
            buildConfigField "String", "BASE_PROVIDER_AUTHORITY", "\"" + BASE_PROVIDER_DEBUG + "\""
        }

        release {
            ext.enableCrashlytics = buildProperties['IS_CRASHLYTICS_ENABLED'].toBoolean()
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            buildConfigField "String", "BASE_PROVIDER_AUTHORITY", "\"" + BASE_PROVIDER_RELEASE + "\""
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    packagingOptions {
        exclude 'META-INF/pom.properties'
        exclude 'META-INF/pom.xml'
    }
}


dependencies {
    implementation 'com.google.android.gms:play-services-base:16.1.0'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-analytics:16.0.6'
    implementation 'com.google.android.gms:play-services-ads:17.1.2'
    implementation 'com.google.android.ads.consent:consent-library:1.0.7'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:percent:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.squareup.okhttp:okhttp:2.6.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit:retrofit:1.9.0'
    implementation 'com.yqritc:recyclerview-multiple-viewtypes-adapter:1.1.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true
    }
}