apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Load application build configuration.
def buildProperties = new Properties()
def buildProdFile = new File('build_prod.properties')
if (buildProdFile.canRead()) {
    buildProperties.load(new FileInputStream(buildProdFile))
} else {
    buildProdFile = new File('build_default.properties')
    buildProperties.load(new FileInputStream(buildProdFile))
}

android {
    final BASE_PROVIDER_DEBUG = "com.markupartist.sthlmtraveling.debug"
    final BASE_PROVIDER_RELEASE = "com.markupartist.sthlmtraveling"

    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        applicationId "com.markupartist.sthlmtraveling"
        versionName "2.19.0"
        versionCode 207

        buildConfigField "boolean", "APP_IS_ADS_ENABLED", buildProperties['IS_ADS_ENABLED']
        buildConfigField "String", "APP_WIDESPACE_SID", buildProperties['WIDESPACE_SID']
        buildConfigField "String", "APP_WIDESPACE_DEBUG_SID", buildProperties['WIDESPACE_DEBUG_SID']
        buildConfigField "String", "APP_STHLMTRAVELING_API_KEY", buildProperties['STHLMTRAVELING_API_KEY']
        buildConfigField "String", "APP_ANALYTICS_PROPERTY_KEY", buildProperties['ANALYTICS_PROPERTY_KEY']

        resValue "string", "google_maps_api_key", buildProperties['GOOGLE_MAPS_API_KEY']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile file(System.getenv().containsKey("STORE_FILE") ? System.getenv("STORE_FILE") : "dummy.keystore")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = buildProperties['IS_CRASHLYTICS_ENABLED'].toBoolean()
            debuggable true
            applicationIdSuffix ".debug"
            buildConfigField "String", "BASE_PROVIDER_AUTHORITY", "\"" + BASE_PROVIDER_DEBUG + "\""
        }

        release {
            ext.enableCrashlytics = buildProperties['IS_CRASHLYTICS_ENABLED'].toBoolean()
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
            buildConfigField "String", "BASE_PROVIDER_AUTHORITY", "\"" + BASE_PROVIDER_RELEASE + "\""
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    packagingOptions {
        exclude 'META-INF/pom.properties'
        exclude 'META-INF/pom.xml'
    }
}


dependencies {
    compile 'com.google.android.gms:play-services-base:11.2.2'
    compile 'com.google.android.gms:play-services-maps:11.2.2'
    compile 'com.google.android.gms:play-services-location:11.2.2'
    compile 'com.google.android.gms:play-services-analytics:11.2.2'
    compile 'com.google.android.gms:play-services-ads:11.2.2'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:percent:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:customtabs:26.1.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.yqritc:recyclerview-multiple-viewtypes-adapter:1.1.0'
    compile 'org.slf4j:slf4j-android:1.7.10'
    compile files('libs/widespace-sdk-4.9.1-98c1ae7.jar')
    compile files('libs/wisper-java-1.0-rc8.jar')
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }
}